plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion app_android.compileSdkVersion
    buildToolsVersion app_android.buildToolsVersion

    defaultConfig {
        applicationId "cn.tim.xchat"
        minSdkVersion app_android.minSdkVersion
        targetSdkVersion app_android.targetSdkVersion
        versionCode app_android.versionCode
        versionName app_android.versionName

        testInstrumentationRunner app_android.testInstrumentationRunner
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11"
            }
        }

        ndk {
            abiFilters  "armeabi-v7a", "arm64-v8a"
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ AROUTER_MODULE_NAME  : project.getName() ]
            }
        }
    }

    buildTypes {
        debug {
            // 测试环境用debug_url
            buildConfigField("String", "SERVER_URL", "\"${app_server_url.debug}\"")
        }

        release {
            // 正式环境用release_url
            buildConfigField("String", "SERVER_URL", "\"${app_server_url.release}\"")
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.10.2"
        }
    }
    compileOptions {
        sourceCompatibility app_android.sourceCompatibility
        targetCompatibility app_android.targetCompatibility
    }
    ndkVersion '22.1.7171670'
}

dependencies {

    app_annotationProcessors.each {k, v ->
        annotationProcessor v
        println('注解处理器: ' + k + " -> " + v)
    }

    implementation project(':common')
    implementation project(':base-network')

    if(isRelease){
        implementation project(':chat')
        implementation project(':personal')
        implementation project(':contacts')
        implementation project(':login')
    }
}