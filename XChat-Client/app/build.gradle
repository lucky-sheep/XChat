

plugins {
    id 'com.android.application'
}
apply plugin: 'com.google.protobuf'

if(!isReleaseBuildType()){
    apply plugin: 'com.didi.dokit'
}


android {
    compileSdkVersion app_android.compileSdkVersion
    buildToolsVersion app_android.buildToolsVersion

    defaultConfig {
        applicationId "cn.tim.xchat"
        minSdkVersion app_android.minSdkVersion
        targetSdkVersion app_android.targetSdkVersion
        versionCode app_android.versionCode
        versionName app_android.versionName

        testInstrumentationRunner app_android.testInstrumentationRunner
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11"
            }
        }

        ndk {
            abiFilters  "armeabi-v7a", "arm64-v8a"
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ AROUTER_MODULE_NAME  : project.getName() ]
            }
        }
    }

    buildTypes {
        debug {
            // 测试环境用debug_url
            buildConfigField("String", "SERVER_URL", "\"${app_server_url.debug}\"")
            buildConfigField("String", "WEBSOCKET_URL", "\"${app_server_url.wsdebug}\"")
        }

        release {
            // 正式环境用release_url
            buildConfigField("String", "SERVER_URL", "\"${app_server_url.release}\"")
            buildConfigField("String", "WEBSOCKET_URL", "\"${app_server_url.wsrelease}\"")
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility app_android.sourceCompatibility
        targetCompatibility app_android.targetCompatibility
    }
}

dependencies {
    debugImplementation "io.github.didi.dokit:dokitx:3.5.0"

    app_annotationProcessors.each {k, v ->
        annotationProcessor v
        println('注解处理器: ' + k + " -> " + v)
    }

    implementation project(':common')
    implementation project(':base-network')

    if(isRelease){
        implementation project(':chat')
        implementation project(':personal')
        implementation project(':contacts')
        implementation project(':login')
    }
}




dokitExt {
    //通用设置
    comm {
        //地图经纬度开关
        gpsSwitch true
        //网络开关
        networkSwitch true
        //大图开关
        bigImgSwitch true
        //webView js 抓包
        webViewSwitch true
    }
}

boolean isReleaseBuildType(){
    for(String s : gradle.startParameter.taskNames) {
        if (s.contains("Release") | s.contains("release")) {
            return true
        }
    }
    return false
}